pkgname=rz_report
pkgver=r13.90f514b
pkgrel=1
pkgdesc="Rizin addon to generate reports"
arch=('i686' 'x86_64')
license=('CCPL:cc-by-nc-sa-4.0')
depends=('rizin')
makedepends=('dotnet-sdk')
provides=($pkgname)
conflicts=($pkgname)
source=("$pkgname::git://github.com/raboni84/rz_report.git")
sha256sums=('SKIP')

pkgver(){
  cd $pkgname
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd $pkgname
    git submodule update --init
}

build() {
  cd $pkgname
  rm -rf bin/ obj/
  dotnet build -c Release
}

package() {
  cd $pkgname
  install -d "$pkgdir/opt/$pkgname"
  dotnet publish --no-build -c Release -o "$pkgdir/opt/$pkgname"
  
  install -d "$pkgdir/opt/$pkgname/rules"
  cp -vR rules/* "$pkgdir/opt/$pkgname/rules/"

  install -d "$pkgdir/usr/bin"
  ln -s "/opt/$pkgname/$pkgname" "$pkgdir/usr/bin/$pkgname"

  install -D -m644 "LICENSE" \
  "${pkgdir}/usr/share/licenses/${pkgname}/cc-by-nc-sa-4.0.txt"
}
