pkgname=awesome-git
_pkgname=awesome
pkgver=v4.3.r895.g538586c17
pkgrel=1
pkgdesc="awesome window manager built with luajit"
url="https://awesomewm.org/"
arch=('i686' 'x86_64')
license=('GPL2')
depends=('cairo' 'dbus' 'gdk-pixbuf2' 'libxdg-basedir' 'libxkbcommon-x11'
         'lua53' 'lua53-lgi' 'pango' 'startup-notification' 'xcb-util-cursor'
         'xcb-util-keysyms' 'xcb-util-wm' 'xcb-util-xrm' 'libxfixes')
makedepends=('asciidoc' 'cmake' 'docbook-xsl' 'git' 'imagemagick' 'ldoc'
             'xmlto')
provides=('notification-daemon' 'awesome')
conflicts=('awesome')
backup=('etc/xdg/awesome/rc.lua')
source=("$_pkgname::git+https://github.com/awesomeWM/awesome.git"
        awesome.desktop
        awesome.patch)
sha256sums=('SKIP'
            '5c5437448cc9f01be6ccbb298f5c86d0f8c4bcae23a22e6af699aff0d10f642f'
            'SKIP')
_LUA_VER=5.3

pkgver(){
  cd $_pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
  sed -i "s/COMMAND lua\b/COMMAND lua${_LUA_VER}/" awesomeConfig.cmake tests/examples/CMakeLists.txt
  sed -i "s/LUA_COV_RUNNER lua\b/LUA_COV_RUNNER lua${_LUA_VER}/" tests/examples/CMakeLists.txt
  patch --forward --strip=1 --input="${srcdir}/awesome.patch"
}

build() {
  mkdir -p build
  cd build

  cmake ../$_pkgname \
    -DCMAKE_BUILD_TYPE=RELEASE \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DSYSCONFDIR=/etc \
    -DLUA_INCLUDE_DIR=/usr/include/lua${_LUA_VER} \
    -DLUA_LIBRARY=/usr/lib/liblua.so.${_LUA_VER}
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install

  install -Dm644 "$srcdir"/$_pkgname/awesome.desktop \
    "$pkgdir/usr/share/xsessions/awesome.desktop"
}
