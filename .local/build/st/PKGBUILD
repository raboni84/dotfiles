pkgname=st-git
_pkgname=st
pkgver=0.8.4.r2.g4ef0cbd
pkgrel=1
pkgdesc="A simple terminal implementation for X"
url="http://st.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
depends=('libx11' 'libxinerama' 'libxft' 'freetype2' 'ttf-dejavu')
makedepends=('git')
provides=('st')
conflicts=('st')
source=("$_pkgname::git+http://git.suckless.org/st"
        st.patch
        config.h)
md5sums=('SKIP'
         'SKIP'
         'SKIP') # so you can customize config.h

pkgver(){
  cd $_pkgname
  git describe --long --tags | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
  cd $_pkgname
  if [[ -f "$srcdir/config.h" ]]; then
    cp -fv "$srcdir/config.h" config.h
  fi
  patch --forward --strip=1 --input="${srcdir}/st.patch"
}

build() {
  cd $_pkgname
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $_pkgname
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -m644 -D LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -m644 -D README "$pkgdir/usr/share/doc/$pkgname/README"
}
